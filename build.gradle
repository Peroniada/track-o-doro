plugins {
    id 'groovy'
    id 'java'
    id 'idea'
    id "io.freefair.lombok" version "3.8.4"
    id "org.flywaydb.flyway" version "6.1.3"
    id 'nu.studer.jooq' version '4.1'
}

def db_url = System.getenv('DB_URL') ?: "localhost:5432/pomodoro-tracker"

group 'com.sperek'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

flyway {
    url = "jdbc:postgresql://${db_url}"
    user = 'postgres'
    password = 'root'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.sperek.pomodorotracker.application.Main',
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    archiveClassifier.set('all')
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}



artifacts {
    archives fatJar
}

dependencies {
    compile group: 'org.jooq', name: 'jooq', version: '3.12.3'
    compile group: 'org.jooq', name: 'jooq-codegen', version: '3.12.3'
    compile group: 'org.jooq', name: 'jooq-meta', version: '3.12.3'
    compile 'io.javalin:javalin:3.4.1'
    compile "org.slf4j:slf4j-simple:1.7.26"
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.0.8'
    compile group: 'cc.vileda', name: 'kotlin-openapi3-dsl', version: '0.20.1'
    compile group: 'org.webjars', name: 'swagger-ui', version: '3.22.2'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.9'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.10.2'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.10'
    compile 'io.jsonwebtoken:jjwt-api:0.10.7'
    runtime 'io.jsonwebtoken:jjwt-impl:0.10.7',
            // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
            //'org.bouncycastle:bcprov-jdk15on:1.60',
            'io.jsonwebtoken:jjwt-jackson:0.10.7'
    jooqRuntime 'org.postgresql:postgresql:42.2.10'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

jooq {
    version = '3.12.3'
    edition = 'OSS'
    generateSchemaSourceOnCompilation = true
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
//            url = 'jdbc:postgresql://db:5432/pomodoro-tracker'
            url = "jdbc:postgresql://${db_url}"
            user = 'postgres'
            password = 'root'
            properties {
                property {
                    key = 'ssl'
                    value = 'false'
                }
            }
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                // ...
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
                // ...
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
                // ...
            }
            target {
                packageName = 'com.sperek.infrastructure'
                // directory = ...
            }
        }
    }
}